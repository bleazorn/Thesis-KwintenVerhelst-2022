impose-calling-conventions

p	::=	(module (define label (lambda (aloc ...) entry)) ... entry)

entry	::=	tail

pred 	::= 	(relop opand opand)
	|	(true)
	|	(false)
	|	(not pred)
	| 	(begin effect ... pred)
	|	(if pred pred pred)

tail  	::= 	value
	|	(call triv opand ...)
	|	(begin effect ... tail)
	|	(if pred tail tail)	

value	::= 	triv
	|   	(binop opand opand)
	|	(call triv opand ...)

effect	::=	(set! aloc value)
	|	(if pred effect effect)
	|	(begin effect ... effect)
 	 	 
trg	::=	aloc
	|	label

triv	::= 	trg			
	| 	integer

opand	::= 	integer
	|	aloc
 	 		 	 
binop	::=	+
 	|	*
	|	-

relop	::= 	<
	|	<=
	|	=
	|	>=
	|	>
	|	!=
 	 	 	 	 
aloc	::= 	aloc?

label	::= 	label?
 	 	 	 	 
integer	::=	int32?











select-instructions

p	::=	(module info (define label info tail) ... tail)

info	::= 	(#:from-contract (info/c (new-frames (frame ...))))

frame	::=	(label? (aloc ...))

pred 	::= 	(relop opand opand)
	|	(true)
	|	(false)
	|	(not pred)
	| 	(begin effect ... pred)
	|	(if pred pred pred)

tail  	::= 	(jump trg loc ...)
	|	(begin effect ... tail)
	|	(if pred tail tail)
	| 	(return-point label tail)	

value	::= 	triv
	|   	(binop opand opand)

effect	::=	(set! loc value)
	|	(if pred effect effect)
	|	(begin effect ... effect)
	| 	(return-point label tail)
 	 	 
trg	::=	loc
	|	label

triv	::= 	trg			
	| 	integer

opand	::= 	integer
	|	loc

loc 	::=	rloc
	|	aloc
 	 		 	 
binop	::=	+
 	|	*
	|	-

relop	::= 	<
	|	<=
	|	=
	|	>=
	|	>
	|	!=

aloc	::= 	aloc?

rloc	::= 	register?
	|	fvar?

label	::= 	label?
 	 	 	 	 
integer	::=	int32?
